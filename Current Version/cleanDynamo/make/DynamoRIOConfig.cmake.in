# **********************************************************
# Copyright (c) 2009 VMware, Inc.    All rights reserved.
# **********************************************************

# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
# 
# * Redistributions of source code must retain the above copyright notice,
#   this list of conditions and the following disclaimer.
# 
# * Redistributions in binary form must reproduce the above copyright notice,
#   this list of conditions and the following disclaimer in the documentation
#   and/or other materials provided with the distribution.
# 
# * Neither the name of VMware, Inc. nor the names of its contributors may be
#   used to endorse or promote products derived from this software without
#   specific prior written permission.
# 
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
# ARE DISCLAIMED. IN NO EVENT SHALL VMWARE, INC. OR CONTRIBUTORS BE LIABLE
# FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
# DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
# SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
# CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT
# LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY
# OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH
# DAMAGE.

###########################################################################
#
# How to use:
#
# Step 1 of 2: load this file in your client's CMakeLists.txt file:
#
#   find_package(DynamoRIO)
#
# You can optionally specify the version number you need:
#
#   find_package(DynamoRIO 1.4)
#
# If DynamoRIO is not installed in standard system directories, set
# the DynamoRIO_DIR variable to tell find_package() where to find
# DynamoRIO's cmake/ directory:
#
#   cmake -DDynamoRIO_DIR=/path/to/dynamorio/cmake <path/to/myclient/sources>
#
# Step 2 of 2: after you've defined your target, configure it using the
# configure_DynamoRIO_client() function defined in this file.
# For example:
#
#  add_library(myclient SHARED myclient.c)
#  configure_DynamoRIO_client(myclient)
#
###########
#
# Standalone usage:
#
# If instead of a client you are using DynamoRIO as a standalone library,
# use the configure_DynamoRIO_standalone() function:
#
#  add_executable(myapp myapp.c)
#  configure_DynamoRIO_standalone(myapp)
#
# If using the drconfig (dr_config.h) and/or drinject (dr_inject.h) API's,
# you also need to explicitly link with those libraries:
#
#  target_link_libraries(myapp drinjectlib drconfiglib)
#
###########
#
# Global changes:
#
# Due to CMake limitations, this file must set some global values:
# - include directories
# - compiler flags
#
# In order to set the compiler flags, the base flags are emptied out:
# - CMAKE_C_FLAGS
# - CMAKE_CXX_FLAGS
# - CMAKE_C_FLAGS_${CMAKE_BUILD_TYPE_UPPER}
# - CMAKE_CXX_FLAGS_${CMAKE_BUILD_TYPE_UPPER}
#
# None of the global changes occur on the find_package(); they are only
# implemented on the first configure_DynamoRIO_*() call.  Optionally,
# they can be triggered earlier by calling configure_DynamoRIO_global().
# The original cflag values are saved in variables with the prefix ORIG_
# followed by the variable name (e.g., ORIG_CMAKE_C_FLAGS).
#
# Various properties of the targets passed to configure_DynamoRIO_*()
# are set, so any prior values will be lost, and if later values are
# added after the configure call they should be appended.
#
###########
#
# Optional parameters:
#
# By default, the RPATH is not set for clients, but is set for standalone usage.
# If this variable is set it overrides the default behavior:
#
#  set(DynamoRIO_RPATH ON)
#
# If you are not exporting all global symbols in your Linux client,
# this file tries to automatically determine that and explicitly mark
# required client exports as visible.  The auto-detection can be
# overridden by setting this variable before calling the
# configure_DynamoRIO_client():
#
#  set(DynamoRIO_VISATT ON)
#
###########################################################################

if (UNIX)
  if (NOT CMAKE_COMPILER_IS_GNUCC)
    # Our linker script is GNU-specific
    message(FATAL_ERROR "DynamoRIO's CMake configuration only supports the GNU linker on Linux")
  endif (NOT CMAKE_COMPILER_IS_GNUCC)
else (UNIX)
  if (NOT ${COMPILER_BASE_NAME} STREQUAL "cl")
    # Our link flags are Microsoft-specific
    message(FATAL_ERROR "DynamoRIO's CMake configuration only supports the Microsoft compiler on Windows")
  endif (NOT ${COMPILER_BASE_NAME} STREQUAL "cl")
endif (UNIX)
    
get_filename_component(cwd "${CMAKE_CURRENT_LIST_FILE}" PATH)

# Export variables in case client needs custom configuration that
# our exported functions do not provide.
# Additionally, only set if not already defined, to allow
# for further customization.
if (NOT DEFINED DynamoRIO_INCLUDE_DIRS)
  set(DynamoRIO_INCLUDE_DIRS "${cwd}/../include")
endif (NOT DEFINED DynamoRIO_INCLUDE_DIRS)

if (UNIX)
  # Wrapping for C++ clients
  # We export the flags as a variable in case client wants to use in custom way
  set(CXX_LIBS
    -print-file-name=libstdc++.a
    -print-file-name=libgcc.a
    -print-file-name=libgcc_eh.a)
  foreach (lib ${CXX_LIBS})  
    execute_process(COMMAND
      ${CMAKE_CXX_COMPILER} ${CMAKE_CXX_FLAGS} ${lib} 
      RESULT_VARIABLE cxx_result
      ERROR_VARIABLE cxx_error
      OUTPUT_VARIABLE string)
    if (cxx_result OR cxx_error)
      message(FATAL_ERROR "*** ${CMAKE_CXX_COMPILER} failed: ***\n${cxx_error}")
    endif (cxx_result OR cxx_error)
    string(REGEX REPLACE "[\r\n]" "" string "${string}")
    set(DynamoRIO_CXX_LINK_FLAGS "${DynamoRIO_CXX_LINK_FLAGS} ${string}")
  endforeach (lib)
  set(DynamoRIO_CXX_LINK_FLAGS
    "${DynamoRIO_CXX_LINK_FLAGS} -Xlinker -wrap=malloc -Xlinker -wrap=realloc -Xlinker -wrap=free")
endif (UNIX)

string(TOUPPER "${CMAKE_BUILD_TYPE}" CMAKE_BUILD_TYPE_UPPER)

###########################################################################
#
# Define functions the client can use to set up build parameters:


# helper function
function (get_lang target lang_var)
  # Note that HAS_CXX and LINKER_LANGUAGE are only defined it
  # explicitly set: can't be used to distinguish CXX vs C.
  get_target_property(sources ${target} SOURCES)
  foreach (src ${sources})
    # LANGUAGE, however, is set for us
    get_source_file_property(src_lang ${src} LANGUAGE)
    if (NOT DEFINED tgt_lang)
      set(tgt_lang ${src_lang})
    elseif (${src_lang} MATCHES CXX)
      # If any source file is cxx, mark as cxx
      set(tgt_lang ${src_lang})
    endif (NOT DEFINED tgt_lang)
  endforeach (src)

  set(${lang_var} ${tgt_lang} PARENT_SCOPE)
endfunction (get_lang)


# helper function
function (get_size is_cxx x64_var)
  if (is_cxx)
    set(sizeof_void ${CMAKE_CXX_SIZEOF_DATA_PTR})
  else (is_cxx)
    set(sizeof_void ${CMAKE_C_SIZEOF_DATA_PTR})
  endif (is_cxx)

  if (${sizeof_void} EQUAL 8)
    set(${x64_var} ON PARENT_SCOPE)
  else (${sizeof_void} EQUAL 8)
    set(${x64_var} OFF PARENT_SCOPE)
  endif (${sizeof_void} EQUAL 8)
endfunction (get_size)


# Unfortunately, CMake doesn't support removing flags on a per-target basis,
# or per-target include dirs or link dirs, so we have to make global changes.
# We don't want find_package() to incur those changes: only if a target
# is actually configured.
function (configure_DynamoRIO_global is_cxx)
  # Should only be done once, but not done at all if no target configured
  if (NOT DynamoRIO_configured_globally)
    # Parent must execute this as well b/c there's no GLOBAL_SCOPE
    set(DynamoRIO_configured_globally ON PARENT_SCOPE)
    # If called from another function, indicate whether to propagate
    # with a variable that does not make it up to global scope
    if (nested_scope)
      set(just_configured ON PARENT_SCOPE)
    endif (nested_scope)

    include_directories(${DynamoRIO_INCLUDE_DIRS})

    # We can't have configure_common() pass in is_x64, since we want to
    # support a user calling us prior to setting up any targets
    get_size(${is_cxx} is_x64)

    if (NOT DynamoRIO_INTERNAL)
      # Define imported target for DynamoRIO library, to allow dependencies on
      # the library and trigger client rebuild on DynamoRIO upgrade:
      # We always link to release build.  At runtime debug build can be
      # swapped in instead.
      # We assume is_x64 can have only one value per configuration.
      if (is_x64)
        include(${cwd}/DynamoRIOTarget64.cmake)
      else (is_x64)
        include(${cwd}/DynamoRIOTarget32.cmake)
      endif (is_x64)
    endif (NOT DynamoRIO_INTERNAL)

    # Remove global C flags that are unsafe for a client library.
    # Since CMake does not support removing flags on a per-target basis,
    # we clear the base flags so we can add what we want to each target.
    foreach (var CMAKE_C_FLAGS;CMAKE_CXX_FLAGS;
        CMAKE_C_FLAGS_${CMAKE_BUILD_TYPE_UPPER};
        CMAKE_CXX_FLAGS_${CMAKE_BUILD_TYPE_UPPER})
      set(ORIG_${var} "${${var}}" PARENT_SCOPE)
      set(local_${var} "${${var}}")
      if (WIN32)
        # We could limit this global var changing to Windows,
        # but it simplifies cross-platform uses to be symmetric
        string(REGEX REPLACE "/M[TD]d?" "" local_${var} "${local_${var}}")
        string(REGEX REPLACE "/RTC." "" local_${var} "${local_${var}}")
      endif (WIN32)
      set(CLIENT_${var} "${local_${var}}" PARENT_SCOPE)
      # If we set to "", the default values come back
      set(${var} " " PARENT_SCOPE)
    endforeach (var)

  endif (NOT DynamoRIO_configured_globally)
endfunction (configure_DynamoRIO_global)


function (configure_DynamoRIO_common target is_client x64_var defs_var link_var)
  get_lang(${target} tgt_lang)
  if (${tgt_lang} MATCHES CXX)
    set(tgt_cxx ON)
  else (${tgt_lang} MATCHES CXX)
    set(tgt_cxx OFF)
  endif (${tgt_lang} MATCHES CXX)

  set(nested_scope ON) # for propagation
  configure_DynamoRIO_global(${tgt_cxx})
  if (just_configured)
    # get around lack of GLOBAL_SCOPE
    set(just_configured ON PARENT_SCOPE)
    foreach (var CMAKE_C_FLAGS;CMAKE_CXX_FLAGS;CMAKE_C_FLAGS_${CMAKE_BUILD_TYPE_UPPER};
        CMAKE_CXX_FLAGS_${CMAKE_BUILD_TYPE_UPPER})
      set(ORIG_${var} "${ORIG_${var}}" PARENT_SCOPE)
      set(CLIENT_${var} "${CLIENT_${var}}" PARENT_SCOPE)
      set(${var} "${${var}}" PARENT_SCOPE)
    endforeach (var)
    set(DynamoRIO_configured_globally ${DynamoRIO_configured_globally} PARENT_SCOPE)
  endif (just_configured)

  if (is_client)
    if (tgt_cxx)
      set(tgt_cflags
        "${CLIENT_CMAKE_CXX_FLAGS} ${CLIENT_CMAKE_CXX_FLAGS_${CMAKE_BUILD_TYPE_UPPER}}")
    else (tgt_cxx)
      set(tgt_cflags
        "${CLIENT_CMAKE_C_FLAGS} ${CLIENT_CMAKE_C_FLAGS_${CMAKE_BUILD_TYPE_UPPER}}")
    endif (tgt_cxx)
  else (is_client)
    if (tgt_cxx)
      set(tgt_cflags
        "${ORIG_CMAKE_CXX_FLAGS} ${ORIG_CMAKE_CXX_FLAGS_${CMAKE_BUILD_TYPE_UPPER}}")
    else (tgt_cxx)
      set(tgt_cflags
        "${ORIG_CMAKE_C_FLAGS} ${ORIG_CMAKE_C_FLAGS_${CMAKE_BUILD_TYPE_UPPER}}")
    endif (tgt_cxx)
    if (WIN32)
      # For standalone, we explicitly link in static libc prior to
      # dynamorio.lib to avoid conflicts w/ ntdll forwards, so we don't
      # support dynamic libc (xref PR 305100)
      if ("${tgt_cflags}" MATCHES "/MD")
        string(REGEX REPLACE "/MD" "/MT" tgt_cflags "${tgt_cflags}")
      else ("${tgt_cflags}" MATCHES "/MD")
        set(tgt_cflags "${tgt_cflags} /MT")
      endif ("${tgt_cflags}" MATCHES "/MD")
    endif (WIN32)
  endif (is_client)

  get_size(${tgt_cxx} tgt_x64)
  if (tgt_x64)
    set(tgt_cflags "${tgt_cflags} -DX86_64")
  else (tgt_x64)
    set(tgt_cflags "${tgt_cflags} -DX86_32")
  endif (tgt_x64)

  if (UNIX)
    set(tgt_cflags "${tgt_cflags} -DLINUX")
    if (is_client)

      if (NOT DEFINED DynamoRIO_VISATT)
        # Try to automatically determine visibility
        if ("${tgt_cflags}" MATCHES "-fvisibility=hidden|-fvisibility=internal")
          set(DynamoRIO_VISATT ON)
        endif()
      endif (NOT DEFINED DynamoRIO_VISATT)
      if (DynamoRIO_VISATT)
        set(tgt_cflags "${tgt_cflags} -DUSE_VISIBILITY_ATTRIBUTES")
      endif (DynamoRIO_VISATT)
      if (tgt_cxx)
        set(tgt_link_flags "${tgt_link_flags} ${DynamoRIO_CXX_LINK_FLAGS}")
      endif (tgt_cxx)

      # avoid SElinux text relocation security violations by explicitly requesting PIC
      set(tgt_link_flags 
        "${tgt_link_flags} -fPIC -shared -nostartfiles -nodefaultlibs -lgcc")

      # i#163: avoid stack-check feature that relies on separate library
      execute_process(COMMAND
        ${CMAKE_C_COMPILER} -v --help
        RESULT_VARIABLE gcc_result
        ERROR_QUIET
        OUTPUT_VARIABLE gcc_out)
      if (gcc_result)
        message(FATAL_ERROR "*** ${CMAKE_C_COMPILER} failed to run ***\n")
      endif (gcc_result)
      string(REGEX MATCH "fstack-protector" flag_present "${gcc_out}")
      if (flag_present)
        set(tgt_cflags "${tgt_cflags} -fno-stack-protector")
      endif (flag_present)

    endif (is_client)
    # gcc is invoked for the link step so we have to repeat cflags as well
    set(tgt_link_flags "${tgt_cflags} ${tgt_link_flags}")
  else (UNIX)
    set(tgt_cflags "${tgt_cflags} -DWINDOWS")
    if (tgt_cxx)
      set(tgt_cflags "${tgt_cflags} /EHsc")
    endif (tgt_cxx)
    if (is_client)
      # Avoid bringing in libc and/or kernel32 for stack checks
      set(tgt_cflags "${tgt_cflags} /GS-")
      # FIXME: why isn't /debug showing up: is it
      if (tgt_cxx)
        # Take advantage of the Windows private loader: no longer need
        # /nodefaultlib or /noentry.  We only do so for C++ clients for now.
        set(tgt_link_flags "${tgt_link_flags}")
      else (tgt_cxx)
        set(tgt_link_flags "${tgt_link_flags} /nodefaultlib /noentry")
      endif (tgt_cxx)
    endif (is_client)
  endif (UNIX)

  # DynamoRIOTarget.cmake added the "dynamorio" imported target
  target_link_libraries(${target} dynamorio)

  if (DEFINED DynamoRIO_RPATH)
    set(use_rpath ${DynamoRIO_RPATH})
  else (DEFINED DynamoRIO_RPATH)
    if (is_client)
      # We don't want an rpath as it makes it hard to switch
      # between debug and release libraries at runtime
      set(use_rpath OFF)
    else (is_client)
      # Standalone app is run without drdeploy script to set
      # LD_LIBRARY_PATH, so default to rpath.  Even though it
      # makes it more painful to switch to the debug library,
      # that's rarely needed for standalone.
      set(use_rpath ON)
    endif (is_client)
  endif (DEFINED DynamoRIO_RPATH)
  if (use_rpath)
    get_target_property(libdir dynamorio LOCATION)
    set_target_properties(${target} PROPERTIES
      INSTALL_RPATH ${libdir})
  else (use_rpath)
    set_target_properties(${target} PROPERTIES
      SKIP_BUILD_RPATH ON)
  endif (use_rpath)

  # Pass data to caller
  set(${x64_var} ${tgt_x64} PARENT_SCOPE)
  set(${defs_var} "${tgt_cflags}" PARENT_SCOPE)
  set(${link_var} "${tgt_link_flags}" PARENT_SCOPE)

endfunction (configure_DynamoRIO_common)


function (configure_DynamoRIO_client target)
  configure_DynamoRIO_common(${target} ON tgt_x64 tgt_cflags tgt_link_flags)
  if (just_configured)
    # get around lack of GLOBAL_SCOPE
    # do NOT set just_configured in global scope
    foreach (var CMAKE_C_FLAGS;CMAKE_CXX_FLAGS;CMAKE_C_FLAGS_${CMAKE_BUILD_TYPE_UPPER};
        CMAKE_CXX_FLAGS_${CMAKE_BUILD_TYPE_UPPER})
      set(ORIG_${var} "${ORIG_${var}}" PARENT_SCOPE)
      set(CLIENT_${var} "${CLIENT_${var}}" PARENT_SCOPE)
      set(${var} "${${var}}" PARENT_SCOPE)
    endforeach (var)
    set(DynamoRIO_configured_globally ${DynamoRIO_configured_globally} PARENT_SCOPE)
  endif (just_configured)

  if (tgt_x64)
    # It's currently required on 64bit that a client library have a
    # preferred base address in the lower 2GB, to be reachable by our
    # library and code cache.  This is due to limitations in the current
    # experimental 64bit release and will be adressed in later releases.
    # Naturally for multiple clients different addresses should be used.
    # We suggest using the range 0x72000000-0x75000000.
    set(PREFERRED_BASE 0x72000000)
    if (UNIX)
      # We use a linker script to set the preferred base
      set(LD_SCRIPT ${CMAKE_CURRENT_BINARY_DIR}/ldscript)
      # We do NOT add ${LD_SCRIPT} as an ADDITIONAL_MAKE_CLEAN_FILES since it's
      # configure-time built not make-time built
      set(LD_FLAGS "-melf_x86_64")

      # In order to just tweak the default linker script we start with exactly that.
      execute_process(COMMAND
        ${CMAKE_LINKER} "${LD_FLAGS}" --verbose
        RESULT_VARIABLE ld_result
        ERROR_VARIABLE ld_error
        OUTPUT_VARIABLE string)
      if (ld_result OR ld_error)
        message(FATAL_ERROR "*** ${CMAKE_LINKER} failed: ***\n${ld_error}")
      endif (ld_result OR ld_error)

      # Strip out just the SECTIONS{} portion
      string(REGEX REPLACE ".*(SECTIONS.*\n\\}).*" "\\1" string "${string}")
      # Find and replace the default base
      string(REGEX MATCH "= *[^{\\.\n]+(0x[0-9]+)\\)? *\\+ *SIZEOF_HEADERS"
        default_base "${string}")
      if ("${default_base}" STREQUAL "")
        message(FATAL_ERROR "unsupported ld version: please file this bug")
      endif()
      string(REGEX REPLACE ".*(0x[0-9]+).*" "\\1" default_base "${default_base}")
      string(REGEX REPLACE "${default_base}" "${PREFERRED_BASE}" string "${string}")
      string(REGEX REPLACE "(\n{)" "\\1\n  . = ${PREFERRED_BASE};" string "${string}")
      file(WRITE ${LD_SCRIPT} "${string}")

      # -dT is preferred, available on ld 2.18+: we could check for it
      set(LD_SCRIPT_OPTION "-T")
      set(PREFERRED_BASE_FLAGS "-Xlinker ${LD_SCRIPT_OPTION} -Xlinker \"${LD_SCRIPT}\"")
    else (UNIX)
      set(PREFERRED_BASE_FLAGS "/base:${PREFERRED_BASE} /fixed")
    endif (UNIX)
    set(tgt_link_flags "${tgt_link_flags} ${PREFERRED_BASE_FLAGS}")
  endif (tgt_x64)

  set_target_properties(${target} PROPERTIES
    COMPILE_FLAGS "${tgt_cflags}"
    LINK_FLAGS "${tgt_link_flags}")

  # TODO: a nice feature would be to check the client for libc imports or
  # other not-recommended properties

endfunction (configure_DynamoRIO_client)


function (configure_DynamoRIO_standalone target)
  configure_DynamoRIO_common(${target} OFF tgt_x64 tgt_cflags tgt_link_flags)
  # get around lack of GLOBAL_SCOPE
  foreach (var CMAKE_C_FLAGS;CMAKE_CXX_FLAGS;CMAKE_C_FLAGS_${CMAKE_BUILD_TYPE_UPPER};
      CMAKE_CXX_FLAGS_${CMAKE_BUILD_TYPE_UPPER})
    set(ORIG_${var} "${ORIG_${var}}" PARENT_SCOPE)
    set(CLIENT_${var} "${CLIENT_${var}}" PARENT_SCOPE)
    set(${var} "${${var}}" PARENT_SCOPE)
  endforeach (var)
  set(DynamoRIO_configured_globally ${DynamoRIO_configured_globally} PARENT_SCOPE)

  if (WIN32)
    # PR 305100: must list libc before dynamorio.lib to avoid complaints about
    # the ntdll forwards conflicting w/ libc
    set(tgt_link_flags "libcmt.lib ${tgt_link_flags}")
  endif (WIN32)

  set_target_properties(${target} PROPERTIES
    COMPILE_FLAGS "${tgt_cflags} -DDYNAMORIO_STANDALONE"
    LINK_FLAGS "${tgt_link_flags}")

endfunction (configure_DynamoRIO_standalone)

###########################################################################
#

# DynamoRIO Extensions
function (use_DynamoRIO_extension target extname)
  if (NOT DynamoRIO_INTERNAL)
    # Extensions are exported in the same cmake file as DynamoRIO
    # so we do not need to include another file here.
    # FIXME: should we add explicit support for 3rd party Extensions
    # that are not checked in to public svn by searching here for
    # ${cwd}/../ext/cmake/*.cmake or something?
    include_directories(${cwd}/../ext/include)
  else (NOT DynamoRIO_INTERNAL)
    # support building from build dir
    if (EXISTS "${PROJECT_SOURCE_DIR}/ext")
      include_directories(${PROJECT_SOURCE_DIR}/ext/${extname})
    elseif (EXISTS "${PROJECT_SOURCE_DIR}/../../ext")
      # A hack to support samples being a separate project: probably it
      # should NOT be separate inside DR, and the exported CMakeLists.txt
      # should add a project declaration.  An alternative is to
      # copy ext headers to <builddir>/ext/include and not even check
      # DynamoRIO_INTERNAL.
      include_directories(${PROJECT_SOURCE_DIR}/../../ext/${extname})
    endif ()
  endif (NOT DynamoRIO_INTERNAL)

  target_link_libraries(${target} ${extname})

endfunction (use_DynamoRIO_extension)
